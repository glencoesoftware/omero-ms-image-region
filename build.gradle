plugins {
    id 'application'
    id 'eclipse'
}

group = 'com.glencoesoftware.omero'
version = '0.5.4-SNAPSHOT-31'

mainClassName = 'com.glencoesoftware.omero.ms.image.region.OmeroVertxLauncher'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
    maven {
        name 'Unidata'
        url 'https://artifacts.unidata.ucar.edu/content/repositories/unidata-releases'
    }
    maven {
        url 'https://artifacts.glencoesoftware.com/artifactory/ome.releases'
    }
    maven {
        url 'https://artifacts.glencoesoftware.com/artifactory/gs-omero-snapshots-local'
    }
    maven {
        url 'https://maven.scijava.org/content/groups/public'
    }
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
    maven {
        name 'Brockmann-Consult Public Maven Repository'
        url 'http://nexus.senbox.net/nexus/content/groups/public/'
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'minutes'
    exclude group: 'com.drewnoakes'
    exclude group: 'com.jamonapi'
    exclude group: 'com.zeroc', module: 'icestorm'
    exclude group: 'commons-beanutils'
    exclude group: 'geronimo-spec'
    exclude group: 'gnu.getopt'
    exclude group: 'net.sf.ehcache'
    exclude group: 'org.apache.pdfbox'
    exclude group: 'org.apache.xmlgraphics'
    exclude group: 'org.ini4j'
    exclude group: 'org.javassist'
    exclude group: 'org.quartz-scheduler'
    exclude group: 'org.subethamail'
    exclude group: 'xerces'
    exclude group: 'xalan'
    exclude group: 'edu.ucar', 'module': 'cdm'
}

dependencies {
    implementation 'io.prometheus:simpleclient_vertx:0.6.0'
    implementation 'io.zipkin.brave:brave:5.6.8'
    implementation 'io.zipkin.brave:brave-http:4.13.6'
    implementation 'io.zipkin.brave:brave-instrumentation-http:5.6.8'
    implementation 'io.zipkin.reporter2:zipkin-sender-okhttp3:2.10.0'
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    implementation 'org.slf4j:log4j-over-slf4j:1.7.30'
    implementation 'com.glencoesoftware.omero:omero-ms-core:0.5.2'
    implementation 'io.vertx:vertx-web:3.8.1'
    implementation 'io.vertx:vertx-config:3.8.1'
    implementation 'io.vertx:vertx-config-yaml:3.8.1'
    implementation 'io.vertx:vertx-micrometer-metrics:3.8.5'
    implementation 'io.micrometer:micrometer-registry-prometheus:1.3.0'
    implementation 'org.openmicroscopy:omero-blitz:5.5.6'
    implementation 'io.prometheus.jmx:collector:0.12.0'
    implementation 'io.prometheus:simpleclient_hotspot:0.8.0'
    implementation 'info.picocli:picocli:4.1.4'
    implementation 'com.univocity:univocity-parsers:2.8.3'
    implementation 'com.zeroc:icegrid:3.6.4'
    implementation 'io.tiledb:tiledb-java:0.3.3-SNAPSHOT'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.bc.zarr:jzarr:0.3.2'
    implementation 'org.lasersonlab:s3fs:2.2.3'
    testImplementation 'org.testng:testng:6.10'
    testImplementation 'org.mockito:mockito-core:2.+'
}

jar {
    manifest {
        attributes(
            "Main-Class": mainClassName,
            "Main-Verticle": "com.glencoesoftware.omero.ms.image.region.ImageRegionMicroserviceVerticle",
            "Implementation-Version": archiveVersion
        )
    }
}

test {
  // enable TestNG support (default is JUnit)
  useTestNG()
}

distributions {
    main {
        contents {
            from("$projectDir") {
                include 'README.md'
            }
        }
    }
}

task memoRegenStartScript(type: CreateStartScripts) {
    mainClassName = "com.glencoesoftware.omero.ms.image.region.MemoRegenerator"
    classpath = startScripts.classpath
    outputDir = startScripts.outputDir
    applicationName = "memoregenerator"
}

applicationDistribution.into("bin") {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(memoRegenStartScript)
    fileMode = 0755
}


applicationDefaultJvmArgs = [
    "-Dvertx.logger-delegate-factory-class-name=io.vertx.core.logging.SLF4JLogDelegateFactory"
]
