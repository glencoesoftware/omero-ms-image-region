plugins {
    id 'application'
    id 'eclipse'
}

group = 'com.glencoesoftware.omero'
version = '0.11.0'

mainClassName = 'com.glencoesoftware.omero.ms.image.region.OmeroVertxLauncher'

sourceCompatibility = 1.11
targetCompatibility = 1.11

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        name 'Unidata'
        url 'https://artifacts.glencoesoftware.com/artifactory/unidata-releases'
    }
    maven {
        url 'https://artifacts.glencoesoftware.com/artifactory/ome.releases'
    }
    maven {
        url 'https://artifacts.glencoesoftware.com/artifactory/ome.external'
    }
    maven {
        url 'https://artifacts.glencoesoftware.com/artifactory/gs-omero-snapshots-local'
    }
    maven {
        url 'https://repo.glencoesoftware.com/repository/bioformats2raw2ometiff/'
    }
    maven {
        url 'https://artifacts.glencoesoftware.com/artifactory/scijava-thirdparty'
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'minutes'
    exclude group: 'com.drewnoakes'
    exclude group: 'com.jamonapi'
    exclude group: 'com.zeroc', module: 'icestorm'
    exclude group: 'commons-beanutils'
    exclude group: 'geronimo-spec'
    exclude group: 'gnu.getopt'
    exclude group: 'net.sf.ehcache'
    exclude group: 'org.apache.pdfbox'
    exclude group: 'org.apache.xmlgraphics'
    exclude group: 'org.ini4j'
    exclude group: 'org.javassist'
    exclude group: 'org.quartz-scheduler'
    exclude group: 'org.subethamail'
    exclude group: 'xerces'
}

dependencies {
    implementation 'io.prometheus:simpleclient_vertx:0.6.0'
    implementation 'io.zipkin.brave:brave:5.6.8'
    implementation 'io.zipkin.brave:brave-http:4.13.6'
    implementation 'io.zipkin.brave:brave-instrumentation-http:5.6.8'
    implementation 'io.zipkin.reporter2:zipkin-sender-okhttp3:2.10.0'
    implementation 'ch.qos.logback:logback-classic:1.3.15'
    implementation 'org.slf4j:log4j-over-slf4j:1.7.32'
    implementation 'com.glencoesoftware.omero:omero-zarr-pixel-buffer:0.5.0'
    implementation 'com.glencoesoftware.omero:omero-ms-core:0.11.0-rc1'
    implementation 'io.vertx:vertx-web:4.5.16'
    implementation 'io.vertx:vertx-config:4.5.16'
    implementation 'io.vertx:vertx-config-yaml:4.5.16'
    implementation 'io.vertx:vertx-micrometer-metrics:4.5.16'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.16.1'
    implementation 'io.micrometer:micrometer-registry-prometheus:1.3.0'
    implementation 'org.openmicroscopy:omero-blitz:5.8.3'
    implementation 'io.prometheus.jmx:collector:0.12.0'
    implementation 'io.prometheus:simpleclient_hotspot:0.8.0'
    implementation 'info.picocli:picocli:4.1.4'
    implementation 'com.univocity:univocity-parsers:2.8.3'
    implementation 'com.zeroc:icegrid:3.6.5'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.github.ben-manes.caffeine:caffeine:3.1.8'
    implementation 'dev.zarr:jzarr:0.4.2'
    implementation 'org.lasersonlab:s3fs:2.2.3'
    implementation 'com.amazonaws:aws-java-sdk-s3:1.12.659'

    testImplementation("junit:junit:4.12")
    testImplementation 'org.mockito:mockito-core:2.28.2'
    testImplementation 'com.glencoesoftware:bioformats2raw:0.9.1'
    testImplementation 'info.picocli:picocli:4.7.5'
}

jar {
    manifest {
        attributes(
            "Main-Class": mainClassName,
            "Main-Verticle": "com.glencoesoftware.omero.ms.image.region.ImageRegionMicroserviceVerticle",
            "Implementation-Version": archiveVersion
        )
    }
}

test {
  useJUnit()
}

distributions {
    main {
        contents {
            from("$projectDir") {
                include 'README.md'
            }
        }
    }
}

startScripts {
  classpath  = files('$APP_HOME/lib/*')
}

task memoRegenStartScript(type: CreateStartScripts) {
    mainClass = "com.glencoesoftware.omero.ms.image.region.MemoRegenerator"
    classpath = startScripts.classpath
    outputDir = startScripts.outputDir
    applicationName = "memoregenerator"
}

applicationDistribution.into("bin") {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(memoRegenStartScript)
    fileMode = 0755
}


applicationDefaultJvmArgs = [
    "-Dvertx.logger-delegate-factory-class-name=io.vertx.core.logging.SLF4JLogDelegateFactory"
]
