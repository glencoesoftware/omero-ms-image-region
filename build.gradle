plugins {
    id 'application'
    id 'eclipse'
    id 'java'
}

group = 'com.glencoesoftware.omero'
version = '0.5.0'

mainClassName = 'io.vertx.core.Launcher'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    jcenter()
    maven {
        name 'Unidata'
        url 'https://artifacts.unidata.ucar.edu/content/repositories/unidata-releases'
    }
    maven {
        url 'http://artifacts.openmicroscopy.org/artifactory/maven/'
    }
    maven {
        url 'https://artifacts.glencoesoftware.com/artifactory/gs-omero-snapshots-local'
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'minutes'
    exclude group: 'antlr'
    exclude group: 'asm'
    exclude group: 'backport-util-concurrent'
    exclude group: 'batik'
    exclude group: 'cglib'
    exclude group: 'checkstyle'
    exclude group: 'com.jamonapi'
    exclude group: 'com.jgoodies'
    exclude group: 'com.google.inject'
    exclude group: 'com.mortennobel'
    exclude group: 'com.zeroc'
    exclude group: 'commons-beanutils'
    exclude group: 'commons-codec'
    exclude group: 'commons-collections'
    exclude group: 'commons-io'
    exclude group: 'commons-lang'
    exclude group: 'commons-pool'
    exclude group: 'edu.ucar', module: 'grib'
    exclude group: 'edu.ucar', module: 'bufr'
    exclude group: 'freemarker'
    exclude group: 'hsqldb'
    exclude group: 'janino'
    exclude group: 'javassist'
    exclude group: 'jmock'
    exclude group: 'geronimo-spec'
    exclude group: 'log4j'
    exclude group: 'net.sourceforge.findbugs'
    exclude group: 'omero', module: 'omero-shares'
    exclude group: 'org.apache.ant'
    exclude group: 'org.apache.lucene'
    exclude group: 'org.hibernate', module: 'hibernate-search'
    exclude group: 'org.hibernate', module: 'jtidy'
    exclude group: 'org.subethamail'
    exclude group: 'org.uncommons', module: 'reportng'
    exclude group: 'oro'
    exclude group: 'pdfbox'
    exclude group: 'quartz'
    exclude group: 'velocity-tools'
    exclude group: 'zeroc'
}

dependencies {
    compile 'ch.qos.logback:logback-classic:1.1.7'
    compile ('com.glencoesoftware.omero:omero-ms-core:0.5.0-SNAPSHOT') {
        exclude group: 'org.testng', module: 'testng'
    }
    compile 'io.vertx:vertx-config:3.6.3'
    compile 'io.vertx:vertx-config-yaml:3.6.3'
    compile 'io.vertx:vertx-hazelcast:3.6.3'
    compile ('omero:server:5.4.10-ice36-b105') {
        exclude group: 'org.testng', module: 'testng'
    }
    testCompile 'org.testng:testng:6.10'
    testCompile 'org.mockito:mockito-core:2.+'
}

jar {
    manifest {
        attributes(
            "Main-Class": mainClassName,
            "Main-Verticle": "com.glencoesoftware.omero.ms.image.region.ImageRegionMicroserviceVerticle",
            "Implementation-Version": version
        )
    }
}

test {
  // enable TestNG support (default is JUnit)
  useTestNG()
}

distributions {
    main {
        contents {
            from("$projectDir") {
                include 'README.md'
            }
        }
    }
}

applicationDefaultJvmArgs = [
    "-Dvertx.logger-delegate-factory-class-name=io.vertx.core.logging.SLF4JLogDelegateFactory",
    "-Dhazelcast.logging.type=slf4j"
]
